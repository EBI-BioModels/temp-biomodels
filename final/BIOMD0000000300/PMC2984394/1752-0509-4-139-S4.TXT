# ADDITIONAL FILE 4. The XPP-AUT Model File.

# This code produces the curves shown in Figure 5B, left hand panel and right hand panel, with the parameter set given.

# ODEs

Htot' = SKM1*(1 - Htot*(1 + vPH))
H' = SKM1*(1 - H*(1 + vPH + vFH))
A' = SKM2*(Atot - A*(1 + vFA))/eps
O2' = 0

# Boolean parameters for switching between Full Model and the Skeleton Models

	p FMK=1, SKM1=1, SKM2=1

# FMK = 1 gives Full Model Kinetics; FMK = 0 gives Michaelis Menten approximation
# SKM1 = 1, SKM2 = 0, FMK = 0 for Skeleton Model 1
# SKM1 = 0, SKM2 = 1, FMK = 0 for Skeleton model 2
# SKM1 = 1, SKM2 = 1, FMK = 1 for the Full Model

#Parameters

p alpha=0.33, KdFH=1, KdFA=1, KdPH=1, KdHRE=0.3, gamma=0.02
p Ptot=0.2, KcatPH=500, Ftot=1, w=1, eps=5, Atot=100

# Algebraic Equations

KiFH = KdFH/KdFA*(KdFA  +  SKM2*(A  +  gamma*(Atot - A)))
KiFA = KdFA/KdFH*(KdFH  +  HF)

HP = 0.5*(Htot - Ptot - KdPH + sqrt((KdPH - Htot + Ptot)^2 + 4*Htot*KdPH))
HF = 0.5*(H - Ftot - KiFH + sqrt((KiFH - H + Ftot)^2 + 4*H*KiFH))

vPH = FMK*kcatPH*Ptot*O2/(1 + O2)/(KdPH + Ptot + HP) + (1 - FMK)*kcatPH*Ptot*O2/(1 + O2)/(KdPH + Htot)
vFH = FMK*KcatPH*w*Ftot*O2/(alpha + O2)/(KiFH + Ftot + HF) + (1 - FMK)*KcatPH*w*Ftot*O2/(alpha + O2)/(KiFH + H)
vFA = KcatPH*w*eps*Ftot*O2/(alpha + O2)/(KiFA + A + gamma*(Atot - A))

# HRE occupancy

aux CAD = H/(KdHRE + Htot)
aux CADOH = (Htot - H)/(KdHRE + Htot)
aux NAD = Htot/(KdHRE + Htot)

# For Plotting

aux O2plot = log(O2)/log(10)
aux ANK = A/Atot
aux FIHFree = (KdFH + HF)/(KiFH + HF)
aux HOH = Htot - H

# Numerics and Plotting

@ total=100,yhi=1.05,xhi=0,xlo=-4,ylo=0,dt=100,bound=1000000, meth=stiff, maxstor=500000
@ range=1,rangeover=O2,rangestep=2000,rangelow=0.0001,rangehigh=1,rangereset=no

@ nplot=5, LT=0
@ xp1=O2plot,yp1=Htot
@ xp2=O2plot,yp2=H
@ xp3=O2plot,yp3=HOH
@ xp4=O2plot,yp4=FIHfree
@ xp5=O2plot,yp5=ANK


done

